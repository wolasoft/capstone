apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

def keystorePropertiesFile = rootProject.file("keystore.properties")

def keystoreProperties = new Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.wolasoft.maplenou"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"

    productFlavors {
        prod {
            dimension 'version'
            applicationId "com.wolasoft.maplenou.prod"
            buildConfigField "String", "HOST", "\"http://wolasoft.pythonanywhere.com/\""
        }

        local {
            dimension 'version'
            applicationId "com.wolasoft.maplenou.local"
            buildConfigField "String", "HOST", "\"http://192.168.0.14:80/\""
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    dataBinding.enabled = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:${rootProject.ext.appCompatVersion}"
    implementation "com.google.android.material:material:${rootProject.ext.supportLibraryVersion}"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.vectordrawable:vectordrawable:${rootProject.ext.supportLibraryVersion}"
    implementation "androidx.legacy:legacy-support-v4:${rootProject.ext.supportLibraryVersion}"
    implementation "androidx.recyclerview:recyclerview:${rootProject.ext.supportLibraryVersion}"
    implementation "androidx.cardview:cardview:${rootProject.ext.supportLibraryVersion}"
    implementation "androidx.appcompat:appcompat:${rootProject.ext.supportLibraryVersion}"
    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test:runner:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.1"

    // custom libraries
    implementation project(":waul")

    // dagger
    implementation "com.google.dagger:dagger-android:${rootProject.ext.daggerVersion}"
    implementation "com.google.dagger:dagger-android-support:${rootProject.ext.daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-android-processor:${rootProject.ext.daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${rootProject.ext.daggerVersion}"

    // gson
    implementation 'com.google.code.gson:gson:2.8.5'

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:${rootProject.ext.retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${rootProject.ext.retrofitVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:3.8.0"

    // picasso
    implementation "com.squareup.picasso:picasso:${rootProject.ext.picassoVersion}"

    // android arch room
    implementation "androidx.lifecycle:lifecycle-extensions:${rootProject.ext.lifeCycleVersion}"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:${rootProject.ext.lifeCycleVersion}"
    implementation "androidx.room:room-runtime:${rootProject.ext.roomVersion}"
    annotationProcessor "androidx.room:room-compiler:${rootProject.ext.roomVersion}"

    //adding paging
    implementation "androidx.paging:paging-runtime:${rootProject.ext.pagingVersion}"

    // others
    implementation "org.apache.commons:commons-lang3:3.0"
    implementation "com.synnapps:carouselview:0.1.5"
    implementation "de.hdodenhof:circleimageview:3.0.0"
    implementation "com.google.android.gms:play-services-ads:17.2.0"
    implementation "com.google.firebase:firebase-core:16.0.9"
    implementation 'com.android.support:multidex:1.0.3'
}
